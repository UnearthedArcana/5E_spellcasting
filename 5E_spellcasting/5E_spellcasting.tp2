BACKUP ~weidu_external/backup/5E_spellcasting~
AUTHOR ~SubtleD~

VERSION ~2.7.1~

ALWAYS

  INCLUDE ~5E_spellcasting/lib/semi_spontaneous.tpa~

  INCLUDE ~5E_spellcasting/comp/5E_casting.tpa~
  
  INCLUDE ~5E_spellcasting/5E_settings.ini~

END

ASK_EVERY_COMPONENT

LANGUAGE
  ~English~
  ~english~
  ~5E_spellcasting/lang/english/setup.tra~

LANGUAGE
  ~French~
  ~french~
  ~5E_spellcasting/lang/french/setup.tra~


//set all Vancian casters to use 5E system____________________________________________
//
BEGIN @100
DESIGNATED 100
LABEL ~D5_5E_CASTING_EVERYONE~

ACTION_IF (FILE_EXISTS_IN_GAME ~d5__fnp_spont_option.d5~) BEGIN
  OUTER_SET 5e_divine = 0
END
ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__fnp_spont_option.d5~) BEGIN
  OUTER_SET 5e_divine = 1
END

LAF semi_spontaneous_casting END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__5E_casting_divine.d5~) BEGIN
  LAF semi_divine_spells END
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__5E_casting_arcane.d5~) BEGIN
  ACTION_IF !(FILE_EXISTS_IN_GAME ~d5_new_identify.d5~) BEGIN
    LAF semi_spont_identify END
  END
  LAF semi_arcane_spells END
END

ACTION_PHP_EACH added_arcane_spls AS added => one BEGIN
    LAF add_semi_spells STR_VAR new_spell = EVAL ~%added%~ cast_spell = EVAL ~%added%~ spell_type = ~arcane~ END
END 

ACTION_IF (5e_divine = 1) BEGIN
  ACTION_PHP_EACH added_divine_spls AS added => one BEGIN
    LAF add_semi_spells STR_VAR new_spell = EVAL ~%added%~ cast_spell = EVAL ~%added%~ spell_type = ~divine~ END
  END 
END

LAF process_semi_spells END

LAF 5E_casting_wizards END

ACTION_IF (FILE_EXISTS_IN_GAME ~d5_bard.2da~) BEGIN
  LAF 5e_mng_bards END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~qdtnb_spelltweaks_clone.qd~) BEGIN
  LAF 5E_revised_simulacrum END
END

LAF 5E_casting_bards END

ACTION_IF (5e_divine = 1) BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~d5__spheres.d5~) BEGIN
    LAF FnP_free_cast END
  END

  LAF 5E_casting_clerics END

  LAF 5E_casting_druids END

  LAF 5E_casting_paladins END

  LAF 5E_casting_rangers END

END


//__________________________________________________________________________________
//__________________________________________________________________________________


BEGIN @300
DESIGNATED 300
FORBID_COMPONENT ~5E_spellcasting.tp2~ ~100~ @3
LABEL ~D5_5E_CASTING_BARDS~

ACTION_IF (FILE_EXISTS_IN_GAME ~d5__fnp_spont_option.d5~) BEGIN
  OUTER_SET 5e_divine = 0
END
ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__fnp_spont_option.d5~) BEGIN
  OUTER_SET 5e_divine = 1
END

ACTION_IF (MOD_IS_INSTALLED ~Might_and_Guile.tp2~ ~400~) BEGIN
  LAF semi_spontaneous_casting END
  ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__5E_casting_divine.d5~) BEGIN
    LAF semi_divine_spells END
  END
END

ACTION_IF !(MOD_IS_INSTALLED ~Might_and_Guile.tp2~ ~400~) BEGIN
  LAF semi_spontaneous_casting INT_VAR include_arcane = 1 END
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__5E_casting_arcane.d5~) BEGIN
  ACTION_IF !(FILE_EXISTS_IN_GAME ~d5_new_identify.d5~) BEGIN
    LAF semi_spont_identify END
  END
  LAF semi_arcane_spells END
END

LAF process_semi_spells END

ACTION_IF (FILE_EXISTS_IN_GAME ~qdtnb_spelltweaks_clone.qd~) BEGIN
  LAF 5E_revised_simulacrum END
END

LAF 5E_casting_bards END

/* don't need this...?
 INCLUDE ~5E_spellcasting/comp/2E_bards_5E_casting.tpa~
 LAF 5E_casting_2E_bards END
*/

ACTION_IF (MOD_IS_INSTALLED ~Might_and_Guile.tp2~ ~400~) BEGIN
  INCLUDE ~5E_spellcasting/comp/revised_bards_5E_casting.tpa~
  LAF 5E_casting_nu_bards END
  COPY ~%MOD_FOLDER%/lib/semi_spont/d5_marker.d5~ ~override/d5__5E_casting_divine.d5~
END

COPY ~%MOD_FOLDER%/lib/semi_spont/d5_marker.d5~ ~override/d5__5E_casting_arcane.d5~
COPY ~%MOD_FOLDER%/lib/semi_spont/d5_marker.d5~ ~override/d5__5E_casting_bards.d5~


//__________________________________________________________________________________
//__________________________________________________________________________________


//bonus casting slots from items - with mem slots___________________________________
//
BEGIN @901
DESIGNATED 901
LABEL ~D5_5E_ITEM_MEM_CASTING_SLOTS~
SUBCOMPONENT @900
FORBID_COMPONENT ~TomeandBlood.tp2~ ~92~ @1
FORBID_COMPONENT ~TomeandBlood.tp2~ ~93~ @1
//REQUIRE_PREDICATE (MOD_IS_INSTALLED ~5E_spellcasting.tp2~ ~100~) @1

ACTION_IF (MOD_IS_INSTALLED ~5E_spellcasting.tp2~ ~100~) BEGIN
 COPY ~%MOD_FOLDER%/lib/semi_spont/d5_marker.d5~ ~override/d5__spont_items_mem.d5~

 LAF arcane_spont_items INT_VAR 5e_plus_mem = 1 END
 LAF divine_spont_items INT_VAR 5e_plus_mem = 1 END
END

//bonus casting slots from items - no mem slots_____________________________________
//
BEGIN @902
DESIGNATED 902
LABEL ~D5_5E_ITEM_CASTING_SLOTS_ONLY~
SUBCOMPONENT @900

ACTION_IF (MOD_IS_INSTALLED ~5E_spellcasting.tp2~ ~100~) BEGIN
 COPY ~%MOD_FOLDER%/lib/semi_spont/d5_marker.d5~ ~override/d5__spont_items_no_mem.d5~

 LAF arcane_spont_items INT_VAR 5e_plus_mem = 0 END
 LAF divine_spont_items INT_VAR 5e_plus_mem = 0 END
END

//__________________________________________________________________________________
//__________________________________________________________________________________


